import ppx


def file_roots(pxid):
    """List the MGF file roots associated with the project repository"""
    proj = ppx.find_project(pxid, repo="PRIDE")
    mgf = proj.remote_files("*293T*.mgf")
    return [f.replace(".mgf", "") for f in mgf]

# File and Project information ------------------------------------------------
PROJECT = "PXD009861"
ROOTS = file_roots(PROJECT)
LIBRARY = "massive_human_hcd_unique_targetdecoy"


# Snakemake rules -------------------------------------------------------------
rule all:
    input: "../results/mass_shifts.png"


rule make_figures:
    input:
        expand("../results/{root}.mztab", root=ROOTS),
        expand("../resources/{root}.tsv", root=ROOTS),
    output:
        "../results/mass_shifts.png"
    conda:
        "envs/plotting.yaml"
    notebook:
        "notebooks/make_figures.ipynb"


rule ann_solo_open:
    input:
        f"../resources/{LIBRARY}.splib",
        "../resources/{root}.mgf",
        "../resources/lib-build.mztab"
    output:
        "../results/{root}.mztab"
    conda:
        "envs/ann_solo.yaml"
    log:
        "logs/ann_solo.{root}.log"
    resources:
        h_rt="7:0:0",
        mfree="12G",
        cpus="4",
        gpgpu="TRUE",
        cuda="1",
    shell:
        """
        cp {input[0]} {input[1]} ../resources/*.idxann \
            ../resources/*.spcfg ${{TMP}} &&
        ann_solo \
            ${{TMP}}/{LIBRARY}.splib \
            ${{TMP}}/{wildcards.root}.mgf \
            {output} \
            --precursor_tolerance_mass 5 \
            --precursor_tolerance_mode ppm \
            --precursor_tolerance_mass_open 500 \
            --precursor_tolerance_mode_open Da \
            --fragment_mz_tolerance 0.02 \
            --allow_peak_shifts \
            --hash_len 400 \
        2> {log}
        """


rule ann_solo_narrow:
    input:
        f"../resources/{LIBRARY}.splib",
    output:
        f"../resources/lib-build.mztab"
    log:
        "logs/narrow-ann_solo.log"
    resources:
        h_rt="7:0:0",
        mfree="4G",
        cpus="12",
    shell:
        """
        cp {input} ${{TMP}} &&
        touch ${{TMP}}/empty.mgf &&
        ann_solo \
            ${{TMP}}/{LIBRARY}.splib \
            ${{TMP}}/empty.mgf \
            {output} \
            --precursor_tolerance_mass 5 \
            --precursor_tolerance_mode ppm \
            --fragment_mz_tolerance 0.02 \
            --hach_len 400 \
            --no_gpu \
            2> {log} &&
        cp ${{TMP}}/*.spcfg ../resources &&
        cp ${{TMP}}/*.idxann ../resources
        """


rule get_msfragger:
    output:
        expand("../resources/{root}.tsv", root=ROOTS)
    run:
        proj = ppx.find_project(PROJECT, local="../resources")
        proj.download([f.split("/")[-1] for f in output])


rule get_mgf:
    output:
        expand("../resources/{root}.mgf", root=ROOTS)
    run:
        proj = ppx.find_project(PROJECT, local="../resources")
        proj.download([f.split("/")[-1] for f in output])


rule get_library:
    output:
        "../resources/{LIBRARY}.splib"
    shell:
        """
        ppx --local ../resources {PROJECT} 'massive*' &&
        cd ../resources &&
        tar -xvf massive*.tar.gz
        """
